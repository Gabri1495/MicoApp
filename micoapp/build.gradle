apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.gsorrentino.micoapp"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 27
        versionName '0.7.0'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', "api_key_release", keystoreProperties['googleMapsKeyRelease']
        }
        debug {
            minifyEnabled false
            debuggable true
            resValue 'string', "api_key_debug", keystoreProperties['googleMapsKeyDebug']
        }
        applicationVariants.all {
            variant ->
                variant.outputs.each {
                    output ->
                        def buildType = "${variant.buildType.name}"
                        if (buildType != "release")
                            buildType = "_" + buildType
                        else
                            buildType = ""
                        def name = "MicoApp${buildType}_${variant.versionName}(${variant.versionCode}).apk"
                        output.outputFileName = name
                }
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha05'
    implementation 'androidx.preference:preference:1.1.0-alpha05'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha07'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha05'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0-alpha05'
    implementation 'com.google.android.gms:play-services-maps:16.1.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    // Room components
    implementation 'androidx.room:room-runtime:2.1.0-rc01'
    annotationProcessor 'androidx.room:room-compiler:2.1.0-rc01'
    // Lifecycle components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha01'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.2.0-alpha01'
}
